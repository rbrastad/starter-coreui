buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://repo.enonic.com/public'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.enonic.xp.app'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Runar Brastad'
    vendorUrl = 'https://github.com/rbrastad'
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"

    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-mail:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-http-client:${xpVersion}"
    include "com.enonic.lib:util:1.1.1"
    include "com.enonic.lib:menu:1.1.1"


    webjar "org.webjars.npm:react:15.3.2"
    webjar "org.webjars.npm:react-dom:15.3.2"
    webjar "org.webjars.npm:reactstrap:4.0.1"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

node {
    version = '6.9.4'
    download = true
}

task dev(type: GulpTask, dependsOn: 'installGulp') {
    args = ["--color", "--gulpfile", "gulpfile.js","watch"]
}

task gulp_build(type: GulpTask, dependsOn: 'installGulp') {
    description = 'Build for production using gulp'
    args = ["--color", "--gulpfile", "gulpfile.js"]
}

task webpack(type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack.js')
}

task webpackDev(type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack.js')
    args = [
            "--config",
            "webpack-dev.config.js"
    ]

}

installGulp.dependsOn 'npmInstall'
dev.dependsOn 'build'

build.dependsOn 'gulp_build'
build.mustRunAfter 'gulp_build'

def bintrayUser = hasProperty('bintrayUser') ? bintrayUser: ''
def bintrayApiKey = hasProperty('bintrayApiKey') ? bintrayApiKey : ''

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publish = true
    pkg {
        repo = 'enonic'
        name = project.appName
        userOrg = 'rbrastad'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/rbrastad/starter-coreui'
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        githubRepo = 'rbrastad/starter-coreui' //Optional Github repository

        version {
            name = project.version
            released = new Date()
            configurations = ['archives']
        }
    }
}
